plugins {
  id 'java'
}

version '1.0-SNAPSHOT'

ext {
  vertxVersion = "3.3.2"
}

repositories {
  jcenter()
}

// compileOnly requires Gradle 2.12+
task docProcessing(type: JavaCompile, group: 'build') {
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.compileOnly
  destinationDir = project.file('src/main/asciidoc')
  options.compilerArgs = [
    "-proc:only",
    "-processor", "io.vertx.docgen.JavaDocGenProcessor",
    "-Adocgen.output=${project.buildDir}/asciidoc",
    "-Adocgen.source=${project.projectDir}/src/main/asciidoc/doc.adoc"
  ]
}

compileJava {
  targetCompatibility = 1.8
  sourceCompatibility = 1.8

  dependsOn docProcessing
}

dependencies {
  compileOnly("io.vertx:vertx-docgen:${vertxVersion}")
}
